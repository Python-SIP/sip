# SIP NSIS installer script.
# 
# Copyright (c) 2013 Riverbank Computing Limited <info@riverbankcomputing.com>
#
# This file is part of SIP.
#
# This copy of SIP is licensed for use under the terms of the SIP License
# Agreement.  See the file LICENSE for more details.
#
# This copy of SIP may also used under the terms of the GNU General Public
# License v2 or v3 as published by the Free Software Foundation which can be
# found in the files LICENSE-GPL2 and LICENSE-GPL3 included in this package.
#
# SIP is supplied WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.


# Include the tools we use.
!include MUI2.nsh
!include LogicLib.nsh


# These will change with different releases.
!define SIP_VERSION         "@RM_RELEASE@"
!define SIP_INSTALLER       ""
#!define SIP_INSTALLER       "-2"
!define SIP_PYTHON_MAJOR    "3"
!define SIP_PYTHON_MINOR    "3"
!define SIP_ARCH            "x64"

# These are all derived from the above.
!define SIP_PYTHON_DIR      "C:\Python${SIP_PYTHON_MAJOR}${SIP_PYTHON_MINOR}"
!define SIP_PYTHON_VERS     "${SIP_PYTHON_MAJOR}.${SIP_PYTHON_MINOR}"
!define SIP_PYTHON_HK       "Software\Python\PythonCore\${SIP_PYTHON_VERS}\InstallPath"
!define SIP_NAME            "SIP v${SIP_VERSION} for Python v${SIP_PYTHON_VERS} (${SIP_ARCH})"
!define SIP_HK_ROOT         "Software\SIP\Py${SIP_PYTHON_VERS}"
!define SIP_HK              "${SIP_HK_ROOT}\InstallPath"

${If} ${SIP_ARCH} == "x64"
    !define SIP_PROGRAM_DIR "$PROGRAMFILES64\SIP"
${Else}
    !define SIP_PROGRAM_DIR "$PROGRAMFILES32\SIP"
${Endif}

# Tweak some of the standard pages.
!define MUI_WELCOMEPAGE_TEXT \
"This wizard will guide you through the installation of ${SIP_NAME}.$\r$\n\
$\r$\n\
FIXME: By default install runtime only.
$\r$\n\
Click Next to continue."

!define MUI_FINISHPAGE_LINK "Get the latest news of SIP here"
!define MUI_FINISHPAGE_LINK_LOCATION "http://www.riverbankcomputing.com"


# Define the product name and installer executable.
Name "SIP"
Caption "${SIP_NAME} Setup"
OutFile "sip-${SIP_VERSION}-Py${SIP_PYTHON_MAJOR}.${SIP_PYTHON_MINOR}-${SIP_ARCH}${SIP_INSTALLER}.exe"


# This is done (along with the use of SetShellVarContext) so that we can remove
# the shortcuts when uninstalling under Vista and Windows 7.  Note that we
# don't actually check if it is successful.
RequestExecutionLevel admin


# The different installation types.  "Minimal" is the runtime environment.
# "Full" is everything. 
InstType "Minimal"
InstType "Full"


# Maximum compression.
SetCompressor /SOLID lzma


# We want the user to confirm they want to cancel.
!define MUI_ABORTWARNING

Function .onInit
    ${If} ${SIP_ARCH} == "x64"
        SetRegView 64
    ${Endif}

    # Check if there is already a version of SIP installed for this version of
    # Python.
    ReadRegStr $0 HKCU "${SIP_HK}" ""

    ${If} $0 == ""
        ReadRegStr $0 HKLM "${SIP_HK}" ""
    ${Endif}

    ${If} $0 != ""
        MessageBox MB_YESNO|MB_DEFBUTTON2|MB_ICONQUESTION \
"A copy of SIP for Python v${SIP_PYTHON_VERS} is already installed in $0 \
and should be uninstalled first.$\r$\n \
$\r$\n\
Do you wish to continue with this installation?" IDYES Overwrite
            Abort
Overwrite:
    ${Endif}

    # Check the right version of Python has been installed.
    ReadRegStr $INSTDIR HKCU "${SIP_PYTHON_HK}" ""

    ${If} $INSTDIR == ""
        ReadRegStr $INSTDIR HKLM "${SIP_PYTHON_HK}" ""
    ${Endif}

    ${If} $INSTDIR == ""
        MessageBox MB_YESNO|MB_ICONQUESTION \
"This copy of SIP has been built against Python v${SIP_PYTHON_VERS} \
(${SIP_ARCH}) which doesn't seem to be installed.$\r$\n\
$\r$\n\
Do you wish to continue with the installation?" IDYES GotPython
            Abort
GotPython:
        StrCpy $INSTDIR "${SIP_PYTHON_DIR}"
    ${Endif}
FunctionEnd


# Define the different pages.
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE ".\LICENSE"
!insertmacro MUI_PAGE_COMPONENTS

!define MUI_DIRECTORYPAGE_TEXT_DESTINATION "Python installation folder"
!define MUI_DIRECTORYPAGE_TEXT_TOP \
"The sip extension module will be installed in the site-packages folder of \
your Python installation.  FIXME: Say where the development tools etc. will go."
!insertmacro MUI_PAGE_DIRECTORY

!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

 
# Other settings.
!insertmacro MUI_LANGUAGE "English"


# Installer sections.

Section "Extension module" SecModule
    SectionIn 1 2 RO

    SetOverwrite on

    SetOutPath $PROGRAMFILES\SIP
    File .\LICENSE
    File .\LICENSE-GPL2
    File .\LICENSE-GPL3

    SetOutPath $INSTDIR\Lib\site-packages
    File .\siplib\sip.pyd
SectionEnd

Section "Developer tools" SecTools
    SectionIn 2

    SetOverwrite on

    SetOutPath $INSTDIR
    File .\sipgen\sip.exe

    SetOutPath $INSTDIR\include
    File .\sipgen\sip.h

    CreateDirectory $INSTDIR\sip
SectionEnd

Section "Documentation" SecDocumentation
    SectionIn 2

    SetOverwrite on

    SetOutPath $PROGRAMFILES\SIP
    File /r .\doc
SectionEnd

Section "Start Menu shortcuts" SecShortcuts
    SectionIn 2

    SetShellVarContext all

    # Make sure this is clean and tidy.
    RMDir /r "$SMPROGRAMS\${SIP_NAME}"
    CreateDirectory "$SMPROGRAMS\${SIP_NAME}"

    IfFileExists "$PROGRAMFILES\SIP\doc" 0 +2
        CreateShortCut "$SMPROGRAMS\${SIP_NAME}\SIP Reference Guide.lnk" "$PROGRAMFILES\SIP\doc\html\index.html"

    CreateShortCut "$SMPROGRAMS\${SIP_NAME}\SIP Homepage.lnk" "http://www.riverbankcomputing.com/software/sip/"

    CreateShortCut "$SMPROGRAMS\${SIP_NAME}\Uninstall SIP.lnk" "$PROGRAMFILES\SIP\Uninstall.exe"
SectionEnd

Section -post
    # Tell Windows about the package.
    WriteRegExpandStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${SIP_NAME}" "UninstallString" '"$PROGRAMFILES\SIP\Uninstall.exe"'
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${SIP_NAME}" "DisplayName" "${SIP_NAME}"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${SIP_NAME}" "DisplayVersion" "${SIP_VERSION}${SIP_INSTALLER}"
    WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${SIP_NAME}" "NoModify" "1"
    WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${SIP_NAME}" "NoRepair" "1"

    # Save the installation directories for the uninstaller.
    ClearErrors
    WriteRegStr HKLM "${SIP_HK}" "" $INSTDIR
    IfErrors 0 +2
        WriteRegStr HKCU "${SIP_HK}" "" $INSTDIR

    # Create the uninstaller.
    WriteUninstaller "$PROGRAMFILES\SIP\Uninstall.exe"
SectionEnd


# Section description text.
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
!insertmacro MUI_DESCRIPTION_TEXT ${SecModule} "The extension module."
!insertmacro MUI_DESCRIPTION_TEXT ${SecTools} "The developer tools."
!insertmacro MUI_DESCRIPTION_TEXT ${SecDocumentation} "The documentation."
!insertmacro MUI_DESCRIPTION_TEXT ${SecShortcuts} \
"This adds shortcuts to your Start Menu."
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function un.onInit
    ${If} ${SIP_ARCH} == "x64"
        SetRegView 64
    ${Endif}

    # Get the SIP installation directory.
    ReadRegStr $INSTDIR HKCU "${SIP_HK}" ""

    ${If} $INSTDIR == ""
        ReadRegStr $INSTDIR HKLM "${SIP_HK}" ""

        ${If} $INSTDIR == ""
            # Try where Python was installed.
            ReadRegStr $INSTDIR HKCU "${SIP_PYTHON_HK}" ""

            ${If} $INSTDIR == ""
                ReadRegStr $INSTDIR HKLM "${SIP_PYTHON_HK}" ""

                ${If} $INSTDIR != ""
                    # Default to where Python should be installed.
                    StrCpy $INSTDIR "${SIP_PYTHON_DIR}\"
                ${Endif}
            ${Endif}
        ${Endif}
    ${Endif}
FunctionEnd


Section "Uninstall"
    SetShellVarContext all

    # The module section.
    RMDir /r $PROGRAMFILES\SIP
    Delete $INSTDIR\Lib\site-packages\sip.pyd

    # The developer tools section.
    Delete $INSTDIR\sip.exe
    Delete $INSTDIR\include\sip.h
    RMDir $INSTDIR\sip

    # The shortcuts section.
    RMDir /r "$SMPROGRAMS\${SIP_NAME}"

    # Clean the registry.
    DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${SIP_NAME}"
    DeleteRegKey HKLM "${SIP_HK_ROOT}"
    DeleteRegKey HKCU "${SIP_HK_ROOT}"
SectionEnd
